main_db -3
base_morph 0
base_mix 0
base_play 0
drums_direct 0
drums_od 0
drums_rec_random 0
drums_db -100
keys_direct 0
keys_db -100
rave_amp 0
rave_mp3 0
rebe_mp3 0
$countMeasures := 0
$speedVariance := 0
keys_out 0.
rave_mp3 50

@tempovar $t(60, 1/4)

whenever getTempo($tempo) {
    let $t.tempo := $tempo
    print set tempo to $tempo
}

whenever observeExt($onset) {
    // print $onset
    antescofo::nextevent
    kill observeExt
}


EVENT 4 REC_CLAPS
base_rec 1
whenever claps($doneClaps) {
    antescofo::nextevent
    kill claps
}

EVENT 4 BASE_MORPH
curve base_mix 0., 50. 3s
3s base_morph 1

$countToStop := -1
whenever morphOnset($onset) {
    $countToStop := 0
}
whenever count($meas) {
    if ($countToStop > -1) {
        $countToStop := $countToStop + 1
    }
    if ($countToStop > 3) {
        print done morphing
        base_morph 0
        antescofo::nextevent
        kill morphOnset
        kill count
    }
}

EVENT 4 REC_DRUMS
$countMeasures := 0
$countToStop := -1
drums_direct 1
drums_rec 1
drums_db 0

loop altBass 2 @sync $t {
    $countMeasures := ($countMeasures + 1) % 4
    if ($countMeasures > 1) {
        base_mix 50.
    } else {
        base_mix 70.
    }
}

whenever drumOnsets($onset) {
    $countToStop := 0
    drums_rec 1
    drums_direct 1
    // main_db -100
    abort stopDrumRec
    print start rec
    group stopDrumRec @sync $t {
        1 print stop rec
        drums_rec 0
        drums_direct 0
        drums_od 1
        main_db -3
        kill altBass
        base_mix 80.
    }
}

whenever stopDrums($meas) {
    if ($countToStop > -1) {
        $countToStop := $countToStop + 1
        print counting $countToStop
    }

    if ($countToStop > 7) {
        print done rec drums
        drums_rec 0
        drums_direct 0
        main_db -3
        antescofo::nextevent
        kill drumOnsets
        kill stopDrums
    }
}

EVENT 4 REC_KEYS_A  // seq 0
$countToStop := -1
keys_db -3
keys_direct 1
keys_rec 1
drums_mod 0.1
keys_seq 0

whenever keysOnsets($onset) {
    $countToStop := 0
    keys_rec 1
    keys_direct 1
    abort stopKeyRec
    group stopKeyRec @sync $t {
        1 print stop rec
        keys_rec 0
        keys_direct 0
        keys_od 1
    }
}

whenever stopKeys($meas) {
    if ($countToStop > -1) {
        $countToStop := $countToStop + 1
    }
    if ($countToStop > 3) {
        print done rec keys A
        keys_rec 0
        antescofo::nextevent
        kill keysOnsets
        kill stopKeys
    }
}

EVENT 4 REC_KEYS_B  // seq 1
$countToStop := -1
keys_direct 1
keys_rec 1
drums_mod 0.3
keys_seq 1

whenever keysOnsets($onset) {
    $countToStop := 0
    keys_rec 1
    keys_direct 1
    // main_db -100
    abort stopKeyRec
    group stopKeyRec @sync $t {
        1 print stop rec
        keys_rec 0
        keys_direct 0
        keys_od 0
    }
}

whenever stopKeys($meas) {
    if ($countToStop > -1) {
        $countToStop := $countToStop + 1
    }
    print counting to stop: $countToStop
    if ($countToStop > 3) {
        print done rec keys B
        keys_rec 0
        main_db -3
        antescofo::nextevent
        kill keysOnsets
        kill stopKeys
    }
}

EVENT 16 DEVELOP
keys_db -6
keys_out 15.
keys_mod 1
curve base_mix 80., 95. 8s
$countMeasures := 0
whenever countMeas($meas) {
    $countMeasures := $countMeasures + 1
    print measures: $countMeasures
    if ($speedVariance > 0.5) {
        keys_seq 1
    } else {
        keys_seq 0
    }
    // if ($countMeasures > 16) {
    //     antescofo::nextevent
    //     kill countMeas
    // }
}

EVENT 4 BREAK
keys_mod 0
$countMeasures := 0
keys_direct 0
keys_seq 1
keys_db -3
drums_db 0
main_db -3
4s curve rave_amp 0., 1. 8s
curve keys_rate 2.,4. 4s
4s whenever next($meas) {
    antescofo::nextevent
    kill next
}

EVENT 32 NEW_MIX
$countMeasures := 0
main_db -3
base_mix 90
curve gated_rave_mix 0., 0.8 40s
4s curve keys_out 0., 75. 20s
keys_rate 2
drums_rec_random 1
whenever changeMix($onset) {
    // do something
    if ($onset == "kick") {
        rhloops_db -3 // random
    } else {
        rhloops_db -12 // random
    }
}


EVENT 8 FADE_IN_REBE
$countMeasures := 0
curve rave_mp3 0., 100. 20s


EVENT 4 FADE_REBE_MP3
$countMeasures := 0
curve rebe_mp3 0., 100. 20s
